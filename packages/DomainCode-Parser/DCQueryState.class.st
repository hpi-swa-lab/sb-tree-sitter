Class {
	#name : #DCQueryState,
	#superclass : #Object,
	#instVars : [
		'currentDecorators',
		'newDecorators',
		'rootBlock',
		'currentShortcut',
		'updateQueued',
		'suggestions',
		'transientReplacements',
		'currentTrigger',
		'querySourceOverride',
		'lastSelection',
		'data',
		'destructors',
		'tokens'
	],
	#category : #'DomainCode-Parser'
}

{ #category : #'as yet unclassified' }
DCQueryState >> addSuggestions: aCollection for: aBlock [

	(suggestions notNil and: [suggestions key = aBlock])
		ifTrue: [suggestions value: suggestions value, aCollection]
		ifFalse: [suggestions := aBlock -> aCollection].
	
	aBlock sandblockEditor suggestionMenu focusOnOpen: true.
	aBlock updateSuggestions
]

{ #category : #accessing }
DCQueryState >> atData: aSymbol [

	^ data at: aSymbol
]

{ #category : #'as yet unclassified' }
DCQueryState >> atData: aSymbol gather: aBlock newestDo: anotherBlock [

	| token |
	token := UUID new.
	tokens at: aSymbol put: token.
	data at: aSymbol put: OrderedCollection new.
	
	aBlock value: [:element |
		(tokens at: aSymbol) = token ifTrue: [
			(data at: aSymbol) add: element.
			anotherBlock value: element]]
]

{ #category : #'as yet unclassified' }
DCQueryState >> atData: aSymbol gather: aBlock newestDo: anotherBlock oldDo: aThirdBlock [

	| token |
	token := UUID new.
	tokens at: aSymbol put: token.
	
	data at: aSymbol ifPresent: [:list | list do: aThirdBlock].
	data at: aSymbol put: OrderedCollection new.
	
	aBlock value: [:element |
		(tokens at: aSymbol) = token ifTrue: [
			(data at: aSymbol) add: element.
			anotherBlock value: element]]
]

{ #category : #'as yet unclassified' }
DCQueryState >> atData: aSymbol get: aBlock newestDo: anotherBlock oldDo: aThirdBlock [

	| token |
	token := UUID new.
	tokens at: aSymbol put: token.
	
	data at: aSymbol ifPresent: aThirdBlock.
	data at: aSymbol put: OrderedCollection new.
	
	aBlock value: [:elements |
		(tokens at: aSymbol) = token ifTrue: [
			(data at: aSymbol) addAll: elements.
			anotherBlock value: elements]]
]

{ #category : #accessing }
DCQueryState >> atData: aSymbol ifAbsentPut: aBlock [

	^ data at: aSymbol ifAbsentPut: aBlock
]

{ #category : #accessing }
DCQueryState >> atData: aSymbol ifPresent: aBlock [

	
	^ data at: aSymbol ifPresent: aBlock
]

{ #category : #accessing }
DCQueryState >> atData: aSymbol ifPresent: aBlock ifAbsent: anotherBlock [

	
	^ data at: aSymbol ifPresent: aBlock ifAbsent: anotherBlock
]

{ #category : #accessing }
DCQueryState >> atData: aSymbol put: anObject [

	^ data at: aSymbol put: anObject
]

{ #category : #'as yet unclassified' }
DCQueryState >> ensureReplacement: aClass for: aBlock embed: aClosure initDo: anotherClosure [

	| replacement |
	replacement := (aBlock class = aClass or: [(aBlock replacedParent ifNotNil: #class) = aClass])
		ifFalse: [ | r hadFocus |
			hadFocus := (aBlock sandblockEditor ifNotNil: #textFocus)
				ifNotNil: [:t | (t ownerSatisfying: [:o | o = aBlock]) notNil]
				ifNil: [false].
			
			r := aClass new.
			aBlock replaceBy: r.
			aBlock replacedParent: r.
			r source: aBlock.
			anotherClosure valueWithArguments: {r}.
			hadFocus ifTrue: [r startInputAtEnd].
			r updateReplacement.
			r]
		ifTrue: [ | r |
			r := aBlock class = aClass ifFalse: [aBlock replacedParent] ifTrue: [aBlock].
			r updateReplacement.
			r].
	(transientReplacements at: currentTrigger) add: replacement
]

{ #category : #'as yet unclassified' }
DCQueryState >> errors [

	^ Array streamContents: [:s | self rootBlock allBlocksDo: [:b | b withDecorator: SBErrorDecorator do: [:d | s nextPut: d] ifAbsent: []]]
]

{ #category : #'as yet unclassified' }
DCQueryState >> errorsDo: aBlock [

	self rootBlock allBlocksDo: [:b | b withDecorator: SBErrorDecorator do: [:d | aBlock value: d] ifAbsent: []]
]

{ #category : #'as yet unclassified' }
DCQueryState >> highlight: aBlock [

	(newDecorators at: aBlock ifAbsentPut: [OrderedCollection new]) add: SBHighlightDecorator new
]

{ #category : #'as yet unclassified' }
DCQueryState >> initialize [

	super initialize.
	
	currentDecorators := Dictionary new.
	transientReplacements := Dictionary new.
	self triggers do: [:t |
		currentDecorators at: t put: WeakKeyDictionary new.
		transientReplacements at: t put: WeakSet new].
	updateQueued := Dictionary new.
	data := Dictionary new.
	destructors := Dictionary new.
	tokens := Dictionary new
]

{ #category : #'as yet unclassified' }
DCQueryState >> onDelete [

	destructors keysAndValuesDo: [:key :block | block value: (data at: key)]
]

{ #category : #shortcuts }
DCQueryState >> onDelete: aSymbol do: aBlock [

	destructors at: aSymbol put: aBlock
]

{ #category : #shortcuts }
DCQueryState >> performShortcut: aSymbol for: aBlock [

	[
		currentShortcut := aSymbol.
		self queriesFor: #shortcut do: [:script |
			DCQuery script: script with: aBlock.
			currentShortcut ifNil: [^ true]]] ensure: [currentShortcut := nil].
	^ false
]

{ #category : #'as yet unclassified' }
DCQueryState >> queriesFor: aSymbol do: aBlock [

	querySourceOverride ifNotNil: [^ querySourceOverride value: aSymbol value: aBlock].
	
	Pragma withPragmasIn: DCBlock class do: [:pragma |
		(pragma keyword = #query: and: [
			pragma arguments first isSymbol
				ifTrue: [pragma arguments first = aSymbol]
				ifFalse: [pragma arguments first includes: aSymbol]]) ifTrue: [aBlock value: (DCBlock perform: pragma selector)]]
]

{ #category : #'as yet unclassified' }
DCQueryState >> querySourceOverride: aBlock [

	querySourceOverride := aBlock
]

{ #category : #'as yet unclassified' }
DCQueryState >> queueUpdateQueriesFor: aSymbol [

	(self triggers anySatisfy: [:t | updateQueued at: aSymbol ifAbsent: [false]]) ifTrue: [^ self].
	
	updateQueued at: aSymbol put: true.
	rootBlock world
		addAlarm: #updatePendingQueries
		withArguments: {}
		for: self
		at: Time millisecondClockValue
]

{ #category : #'as yet unclassified' }
DCQueryState >> replacementDeleted: aReplacement [

	aReplacement passiveUninstall.
	self triggers do: [:t | (transientReplacements at: t) remove: aReplacement ifAbsent: []]
]

{ #category : #'as yet unclassified' }
DCQueryState >> reportError: aDecorator for: aBlock [

	| errors |
	errors := newDecorators at: aBlock ifAbsentPut: [OrderedCollection new].
	(errors noneSatisfy: [:e | e message = aDecorator message]) ifTrue: [errors add: aDecorator]
]

{ #category : #'as yet unclassified' }
DCQueryState >> rerunQueriesFor: aSymbol do: aBlock [

	self assert: currentTrigger isNil.
	currentTrigger := aSymbol.
	
	[self updateTransientReplacementsDuring: [self updateDecoratorsDuring: [aBlock value]]] ensure: [currentTrigger := nil]
]

{ #category : #accessing }
DCQueryState >> rootBlock [

	^ rootBlock
]

{ #category : #accessing }
DCQueryState >> rootBlock: aBlock [

	rootBlock := aBlock
]

{ #category : #'as yet unclassified' }
DCQueryState >> showHoverHint: anObject for: aBlock [

	| cleanup |
	cleanup := [aBlock removeActionsSatisfying: [:a | a == cleanup]].
	aBlock when: #mouseLeave evaluate: cleanup
]

{ #category : #'as yet unclassified' }
DCQueryState >> suggestionsFor: aBlock [

	^ suggestions key = aBlock ifTrue: [suggestions value] ifFalse: [{}]
]

{ #category : #'as yet unclassified' }
DCQueryState >> textQueriesFor: aSymbol do: aBlock [

	querySourceOverride ifNotNil: [^ querySourceOverride value: aSymbol value: aBlock].
	
	Pragma
		withPragmasIn: DCBlock class
		do: [:pragma | (pragma keyword = #query: and: [pragma arguments first = aSymbol and: [pragma method hasPragma: #text]]) ifTrue: [aBlock value: (DCBlock perform: pragma selector)]]
]

{ #category : #'as yet unclassified' }
DCQueryState >> triggers [

	^ #(#shortcut #change #type #selection #doubleClick #open #always #save #hover)
]

{ #category : #shortcuts }
DCQueryState >> tryShortcut: aSymbol do: aBlock [

	currentShortcut = aSymbol ifTrue: [| script |
		script := aBlock value.
		self assert: script isArray description: 'shortcut should return a script array'.
		DCQuery script: script with: self rootBlock sandblockEditor selection source.
		currentShortcut := nil]
]

{ #category : #'as yet unclassified' }
DCQueryState >> updateChangeQueries [

	self queueUpdateQueriesFor: #change.
	self queueUpdateQueriesFor: #type.
	self queueUpdateQueriesFor: #always
]

{ #category : #'as yet unclassified' }
DCQueryState >> updateDecoratorsDuring: aBlock [

	| oldDecorators |
	newDecorators := WeakKeyDictionary new.
	
	aBlock value.
	
	oldDecorators := currentDecorators at: currentTrigger.
	oldDecorators do: [:list | list do: [:d | d detach]].
	newDecorators keysAndValuesDo: [:block :list | list do: [:d | block attachDecorator: d]].
	currentDecorators at: currentTrigger put: newDecorators
]

{ #category : #'as yet unclassified' }
DCQueryState >> updatePendingQueries [

	updateQueued keysAndValuesDo: [:trigger :queued | queued ifTrue: [self updateQueriesFor: trigger]].
	
	" FIXME disallows processing to trigger another update -- intentional? "
	self triggers do: [:t | updateQueued at: t put: false]
]

{ #category : #'as yet unclassified' }
DCQueryState >> updateQueriesFor: aSymbol [

	updateQueued at: aSymbol put: false.
	self rerunQueriesFor: aSymbol do: [
		self
			queriesFor: aSymbol
			do: [:script | self rootBlock allChildrenDo: [:b | b isSandblock ifTrue: [DCQuery script: script with: b]]].
		self
			textQueriesFor: aSymbol
			do: [:script | self rootBlock allChildrenDo: [:b | b isTextMorph ifTrue: [DCQuery script: script with: b]]]]
]

{ #category : #'as yet unclassified' }
DCQueryState >> updateQueriesFor: aSymbol at: aBlock [

	self
		rerunQueriesFor: aSymbol
		do: [self queriesFor: aSymbol do: [:script |
				" a prior script may have removed us from the world "
				aBlock isInWorld ifTrue: [DCQuery script: script with: aBlock]]]
]

{ #category : #'as yet unclassified' }
DCQueryState >> updateSelectionQueries [

	| selection |
	selection := rootBlock sandblockEditor selection.
	lastSelection = selection ifTrue: [^ self].
	(selection notNil and: [(selection hasOwner: rootBlock) not and: [selection ~= rootBlock]]) ifTrue: [^ self].
	
	self queueUpdateQueriesFor: #selection.
	lastSelection := selection
]

{ #category : #'as yet unclassified' }
DCQueryState >> updateTransientReplacementsDuring: aBlock [

	| old new |
	old := transientReplacements at: currentTrigger.
	new := WeakSet new.
	transientReplacements at: currentTrigger put: new.
	aBlock value.
	old do: [:r | (new includes: r) ifFalse: [r passiveUninstall]]
]

{ #category : #'as yet unclassified' }
DCQueryState >> updateTypeQueries [

	suggestions := nil.
	self queueUpdateQueriesFor: #type.
	self queueUpdateQueriesFor: #always
]
